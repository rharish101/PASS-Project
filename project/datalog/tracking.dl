#ifndef TRACKING
#define TRACKING

#include "declarations.dl"

// Used for passing around taintedGlobal
firstStmt(id, blk) :- blockStmt(blk, id), !notFirstStmt(id, blk).
lastStmt(id, blk) :- blockStmt(blk, id), !notLastStmt(id, blk).
notFirstStmt(id, blk) :- blockStmt(blk, id),
                         blockStmt(blk, id_src),
                         follows(id, id_src).
notLastStmt(id, blk) :- blockStmt(blk, id),
                        blockStmt(blk, id_src),
                        follows(id_src, id).
notEmptyBlock(blk) :- blockStmt(blk, _).

// Used for getting taints across function returns
blockFollows(blk, blk) :- block(blk).
blockFollows(blk_next, blk_prev) :- goto(_, blk_prev, blk_next).
blockFollows(blk_next, blk_prev) :- branch(_, _, blk_prev, blk_next, _, _).
blockFollows(blk_next, blk_prev) :- branch(_, _, blk_prev, _, blk_next, _).
blockFollows(blk_next, blk_prev) :- jump(_, blk_prev, _, blk_next).
blockFollows(blk_next, blk_prev) :- blockFollows(blk_mid, blk_prev),
                                    blockFollows(blk_next, blk_mid).

#endif
