#ifndef FUNC_RETURN
#define FUNC_RETURN

#include "../declarations.dl"

// Getting guards
argTaint(blk, stack) :- return(_, blk_src),
                        jump(id_jump, _, blk_fn, blk),
                        blockFollows(blk_src, blk_fn),
                        (argTaint(blk_src, [id_jump, stack]),
                         depth(stack, num),
                         num < 2
                         ;
                         validBlkStack(blk, stack),
                         depth(stack, 2)).

// Getting guards
notGuard(id, stack) :- transferArgument(id_return, id_src, idx),
                       argument(id, blk, idx),
                       return(id_return, blk_src),
                       jump(id_jump, _, blk_fn, blk),
                       blockFollows(blk_src, blk_fn),
                       (notGuard(id_src, [id_jump, stack]),
                        depth(stack, num),
                        num < 2
                        ;
                        validIdStack(id, stack),
                        depth(stack, 2)).

// Getting local taints
taintedLocal(id, blk, src, stack) :- transferArgument(id_return, id_src, idx),
                                     argument(id, blk, idx),
                                     return(id_return, blk_src),
                                     jump(id_jump, _, blk_fn, blk),
                                     blockFollows(blk_src, blk_fn),
                                     (taintedLocal(id_src, blk_src, src, [id_jump, stack]),
                                      depth(stack, num),
                                      num < 2
                                      ;
                                      src = "perm",
                                      validBlkStack(blk, stack),
                                      depth(stack, 2)).

taintedGlobalBlkStart(field, blk, src, stack) :- return(_, blk_src),
                                                 jump(id_jump, _, blk_fn, blk),
                                                 blockFollows(blk_src, blk_fn),
                                                 (taintedGlobalBlkEnd(field, blk_src, src, [id_jump, stack]),
                                                  depth(stack, num),
                                                  num < 2
                                                  ;
                                                  taintedGlobal(field, _, src, _),
                                                  validBlkStack(blk, stack),
                                                  depth(stack, 2)).

#endif
