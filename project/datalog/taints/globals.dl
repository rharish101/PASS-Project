#ifndef GLOBAL_TAINTS
#define GLOBAL_TAINTS

#include "../declarations.dl"

// taintedGlobal rules for block start/end
taintedGlobal(field, id, src, stack) :- taintedGlobalBlkStart(field, blk, src, stack),
                                        firstStmt(id, blk),
                                        !store(id, field, _).
taintedGlobalBlkEnd(field, blk, src, stack) :- taintedGlobal(field, id, src, stack),
                                               lastStmt(id, blk).
taintedGlobalBlkEnd(field, blk, src, stack) :- taintedGlobalBlkStart(field, blk, src, stack),
                                               !notEmptyBlock(blk).

// Apply taintedGlobal when storing
taintedGlobal(field, id, src, stack) :- store(id, field, id_var),
                                        taintedLocal(id_var, blk, src, stack),
                                        blockStmt(blk, id).

// Apply perma-taint when loading perma-tainted globals
taintedGlobalBlkStart(field, blk, "perm", stack) :- permaTainted(field),
                                                    function(blk, _),
                                                    validBlkStack(blk, stack).

// Pass taintedGlobal b/w statements
taintedGlobal(field, id, src, stack) :- taintedGlobal(field, id_src, src, stack),
                                        follows(id, id_src),
                                        !store(id, field, _).

// Pass taintedGlobal for each id for transfers
taintedGlobalBlkStart(field, blk, src, stack) :- (goto(_, blk_src, blk);
                                                  branch(_, _, blk_src, blk, _, _);
                                                  branch(_, _, blk_src, _, blk, _)),
                                                 taintedGlobalBlkEnd(field, blk_src, src, stack),
                                                 (src = "perm";
                                                  src != "perm", argTaint(blk, stack)).

// Save perma-taints when returning
permaTainted(field) :- return(_, blk),
                       taintedGlobalBlkEnd(field, blk, src, nil),
                       (src = "perm";
                        src != "perm", argTaint(blk, stack)).

#endif
