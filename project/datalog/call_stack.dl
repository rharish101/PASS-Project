#ifndef CALL_STACK
#define CALL_STACK

#include "declarations.dl"

// Calculate depth
depth(nil, 0).
depth([id_transfer, tail], num + 1) :- depth(tail, num),
                                       validStack([id_transfer, tail]).

// Whether a stack is valid by itself
validStack(nil).
validStack([id_transfer, nil]) :- jump(id_transfer, _, _, _).
validStack(stack) :- stack = [id_1, [id_2, tail]],
                     jump(id_2, _, blk_fn, _),
                     jump(id_1, blk_src, _, _),
                     blockFollows(blk_src, blk_fn),
                     validStack([id_2, tail]),
                     depth([id_2, tail], num),
                     num < MAX_DEPTH.

// Whether a stack is valid for a block
validBlkStack(blk, nil) :- block(blk).
validBlkStack(blk, stack) :- validStack(stack),
                             stack = [id_transfer, tail],
                             jump(id_transfer, _, blk_fn, _),
                             blockFollows(blk, blk_fn).

// Whether a stack is valid for an ID
validIdStack(id, nil) :- statement(id).
validIdStack(id, stack) :- validBlkStack(blk, stack),
                           blockStmt(blk, id).
validIdStack(id, stack) :- validBlkStack(blk, stack),
                           argument(id, blk, _).

#endif
